; Thue-Morse Fractal
; Author: Ron Gross <chiefninjaofficer@gmail.com>
;------------------------------------------------
;
; Quickrun: http://www.calormen.com/jslogo/#
; 
; This fractal is built on the Thue-Morse integer sequence (0110100110010110...). The usual fractal interperts 0 as "move forward" and 1 as "rotate colckwise by Ï€/3".
;
; https://en.wikipedia.org/wiki/Thue%E2%80%93Morse_sequence#Fractals_and_Turtle_graphics


; Clear screen & consts
cs
ct

make "debug FALSE

# Change this to 0, 1, 2,... to view different Thue-More fractals
make "fractal 0

ifelse :fractal = 0 [ ; http://i.imgur.com/QfsSxvv.png
  make "depth 8
  make "angle0 60
  make "angle1 0
  make "step0 0
  make "step1 1
] [
  ifelse :fractal = 1 [ ; http://i.imgur.com/qMm6Z9f.png
    make "depth 8
    make "angle0 120
    make "angle1 60
    make "step0 1
    make "step1 1
  ] [ ; http://i.imgur.com/XWGwR1F.png
    make "depth 6
    make "angle0 72
    make "angle1 0
    make "step0 0
    make "step1 10    
  ]
]


; ------ Loggin
to log :text
	if :debug [ print :text]
end
to logpos :text
	if :debug [ print pos]
	if :debug [ print :text]
end
logpos ["Logging setup successfully]



; Interpertation of 0 and 1
to do0
  log "do0"
  fd :step0
  left :angle0
end

to do1
  log "do1"
  fd :step1
  right :angle1
end



; Thue-More sequence
to thuemore :n :phase
  IFELSE :n <= 1 [
    IFELSE :phase = 0 [
        do0
    	do1
        do1
        do0
    ] [
        do1
        do0
        do0
        do1
    ]
  ] [
    thuemore (:n - 1) :phase
    thuemore (:n - 1) (1 - :phase)
    thuemore (:n - 1) (1 - :phase)
    thuemore (:n - 1) :phase
  ]
  
end

thuemore :depth 0

hideturtle
